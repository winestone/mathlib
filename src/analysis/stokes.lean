import measure_theory.lebesgue_measure
import measure_theory.box_additive
import analysis.calculus.fderiv_measurable

open measure_theory set function topological_space asymptotics filter
open_locale big_operators topological_space filter

noncomputable theory

variables {E : Type*} [normed_group E] [normed_space â„ E] [second_countable_topology E]
  [complete_space E] [measurable_space E] [borel_space E] {n : â„•}
  {f g : (fin (n + 1) â†’ â„) â†’ fin (n + 1) â†’ E}
  {f' g' : (fin (n + 1) â†’ â„) â†’ ((fin (n + 1) â†’ â„) â†’L[â„] fin (n + 1) â†’ E)}
  {x y : fin (n + 1) â†’ â„}

@[simp] lemma edist_insert_nth (i : fin (n + 1)) (cx cy : â„) (x y : fin n â†’ â„) :
  edist (i.insert_nth cx x) (i.insert_nth cy y) = max (edist cx cy) (edist x y) :=
by simp [edist_pi_def, fin.univ_succ_above _ i, (âˆ˜)]

@[simp] lemma nndist_insert_nth (i : fin (n + 1)) (cx cy : â„) (x y : fin n â†’ â„) :
  nndist (i.insert_nth cx x) (i.insert_nth cy y) = max (nndist cx cy) (nndist x y) :=
by { rw â† ennreal.coe_eq_coe, push_cast, exact edist_insert_nth i cx cy x y }

@[simp] lemma dist_insert_nth (i : fin (n + 1)) (cx cy : â„) (x y : fin n â†’ â„) :
  dist (i.insert_nth cx x) (i.insert_nth cy y) = max (dist cx cy) (dist x y) :=
by { simp only [dist_nndist], exact_mod_cast nndist_insert_nth i cx cy x y }

lemma isometry_insert_nth (i : fin (n + 1)) (c : â„) :
  isometry (i.insert_nth c : (fin n â†’ â„) â†’ (fin (n + 1) â†’ â„)) :=
Î» x y, by simp

lemma continuous_insert_nth (i : fin (n + 1)) (c : â„) :
  continuous (i.insert_nth c : (fin n â†’ â„) â†’ (fin (n + 1) â†’ â„)) :=
(isometry_insert_nth i c).continuous

lemma op_insert_nth (i : fin (n + 1)) (câ‚ câ‚‚ : â„) (xâ‚ xâ‚‚ : fin n â†’ â„) (op : â„ â†’ â„ â†’ â„) :
  (Î» j, op (i.insert_nth câ‚ xâ‚ j) (i.insert_nth câ‚‚ xâ‚‚ j)) =
    i.insert_nth (op câ‚ câ‚‚) (Î» j, op (xâ‚ j) (xâ‚‚ j)) :=
fin.eq_insert_nth_iff.2 $ by simp

@[simp] lemma insert_nth_sub_insert_nth (i : fin (n + 1)) (câ‚ câ‚‚ : â„) (xâ‚ xâ‚‚ : fin n â†’ â„) :
  i.insert_nth câ‚ xâ‚ - i.insert_nth câ‚‚ xâ‚‚ = i.insert_nth (câ‚ - câ‚‚) (xâ‚ - xâ‚‚) :=
op_insert_nth i câ‚ câ‚‚ xâ‚ xâ‚‚ has_sub.sub

@[simp] lemma insert_nth_zero (i : fin (n + 1)) (c : â„) : i.insert_nth c 0 = update 0 i c :=
fin.insert_nth_eq_iff.2 $ by simp [fin.succ_above_ne, pi.zero_def]

lemma smul_update (i : fin (n + 1)) (c y : â„) (x : fin (n + 1) â†’ â„) :
  c â€¢ update x i y = update (c â€¢ x) i (c â€¢ y) :=
funext $ apply_update (Î» _ z, c â€¢ z) x i _

def box_boundary_integral (f : (fin (n + 1) â†’ â„) â†’ fin (n + 1) â†’ E) (x y : fin (n + 1) â†’ â„) : E :=
âˆ‘ i : fin (n + 1),
  ((âˆ« z in Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above), f (fin.insert_nth i (y i) z) i) -
    âˆ« z in Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above), f (fin.insert_nth i (x i) z) i)

lemma integrable_on_face_aux (h : continuous_on f (Icc x y)) {i j : fin (n + 1)}
  {m} (hm : m âˆˆ Icc (x i) (y i)) :
  integrable_on (Î» z, f (fin.insert_nth i m z) j) (Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above)) :=
begin
  refine continuous_on.integrable_on_compact compact_pi_Icc _,
  refine (continuous_apply j).comp_continuous_on
    (h.comp (continuous_insert_nth _ _).continuous_on _),
  exact (Î» z hz, âŸ¨fin.le_insert_nth_iff.2 âŸ¨hm.1, hz.1âŸ©, fin.insert_nth_le_iff.2 âŸ¨hm.2, hz.2âŸ©âŸ©)
end

lemma integrable_on_face_right (hle : x â‰¤ y) (h : continuous_on f (Icc x y)) (i : fin (n + 1)) :
  integrable_on (Î» z, f (fin.insert_nth i (y i) z) i) (Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above)) :=
integrable_on_face_aux h (right_mem_Icc.2 (hle i))

lemma integrable_on_face_left (hle : x â‰¤ y) (h : continuous_on f (Icc x y)) (i : fin (n + 1)) :
  integrable_on (Î» z, f (fin.insert_nth i (x i) z) i) (Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above)) :=
integrable_on_face_aux h (left_mem_Icc.2 (hle i))

/-- Divergence theorem for an affine map. -/
lemma affine_map.box_boundary_integral (hxy : x â‰¤ y)
  (f : (fin (n + 1) â†’ â„) â†’áµƒ[â„] (fin (n + 1) â†’ E)) :
  box_boundary_integral f x y = (âˆ i, (y i - x i)) â€¢ âˆ‘ i, f.linear (update 0 i 1) i :=
begin
  have hf : continuous f := f.continuous_of_finite_dimensional,
  rw finset.smul_sum,
  refine finset.sum_congr rfl (Î» i hi, _),
  rw [â† integral_sub (integrable_on_face_right hxy hf.continuous_on i)
    (integrable_on_face_left hxy hf.continuous_on i)],
  have : âˆ€ (i : fin (n + 1)) z, f (i.insert_nth (y i) z) i - f (i.insert_nth (x i) z) i =
    (y i - x i) â€¢ f.linear (update 0 i 1) i,
  { intros i z,
    calc (f (i.insert_nth (y i) z) - f (i.insert_nth (x i) z)) i
        = f.linear (i.insert_nth (y i) z -áµ¥ i.insert_nth (x i) z) i :
          by rw [f.linear_map_vsub, vsub_eq_sub]
    ... = f.linear (update 0 i ((y i - x i) â€¢ (1 : â„))) i : by simp
    ... = ((y i - x i) â€¢ f.linear (update 0 i 1)) i :
          by simp [â† f.linear.map_smul (y i - x i), smul_update] },
  simp only [this, set_integral_const],
  rw [real.volume_Icc_pi_to_real, fin.prod_univ_succ_above _ i, smul_smul, mul_comm],
  exact Î» j, hxy _
end

theorem box_additive_on_box_boundary_integral {x y : fin (n + 1) â†’ â„}
  (hf : continuous_on f (Icc x y)) :
  box_additive_on (box_boundary_integral f) (Icc x y) :=
begin
  refine box_additive_on_sum_faces_fin (Icc x y)
    (Î» (i : fin (n + 1)) c (l r : fin n â†’ â„), âˆ« z in Icc l r, f (i.insert_nth c z) i) (Î» i c, _),
  simp only [volume_pi],
  refine box_additive_on_integral_Icc (Î» _, volume) _,
  by_cases hc : c âˆˆ Icc (x i) (y i),
  { rw [fin.preimage_insert_nth_Icc_of_mem hc],
    exact integrable_on_face_aux hf hc },
  { rw [fin.preimage_insert_nth_Icc_of_not_mem hc],
    exact integrable_on_empty }
end

theorem box_boundary_integral_sub {x y : fin (n + 1) â†’ â„} (h : x â‰¤ y)
  (hfc : continuous_on f (Icc x y)) (hgc : continuous_on g (Icc x y)) :
  box_boundary_integral (Î» z, f z - g z) x y =
    box_boundary_integral f x y - box_boundary_integral g x y :=
begin
  have H : âˆ€ {i} (m âˆˆ Icc (x i) (y i)),
    âˆ« z in Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above),
      f (i.insert_nth m z) i - g (i.insert_nth m z) i =
    (âˆ« z in Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above), f (i.insert_nth m z) i) -
      âˆ« z in Icc (x âˆ˜ i.succ_above) (y âˆ˜ i.succ_above), g (i.insert_nth m z) i,
    from Î» i m hm, integral_sub (integrable_on_face_aux hfc hm) (integrable_on_face_aux hgc hm),
  simp only [box_boundary_integral, H (x _) (left_mem_Icc.mpr (h _)),
    H (y _) (right_mem_Icc.mpr (h _)), pi.sub_apply, finset.sum_sub_distrib],
  abel
end

theorem box_boundary_integral_eq_of_has_fderiv_within_at (h : x â‰¤ y)
  (hf'c : continuous_on (Î» z, âˆ‘ i, f' z (update 0 i 1) i) (Icc x y))
  (hd : âˆ€ z âˆˆ Icc x y, has_fderiv_within_at f (f' z) (Icc x y) z) :
  box_boundary_integral f x y = âˆ« z in Icc x y, âˆ‘ i, f' z (update 0 i 1) i :=
begin
  have hfc : continuous_on f (Icc x y) := Î» z hz, (hd z hz).continuous_within_at,
  set divf := Î» z, âˆ‘ i, f' z (update 0 i 1) i,
  have : box_additive_on (Î» l u, (âˆ« z in Icc l u, divf z) - box_boundary_integral f l u) (Icc x y),
    from (box_additive_on_integral_Icc _ (hf'c.integrable_on_compact compact_pi_Icc)).sub
      (box_additive_on_box_boundary_integral hfc),
  rw [eq_comm, â† sub_eq_zero],
  rcases em (âˆƒ i, x i = y i) with âŸ¨i, hiâŸ© | hne, from this.eq_zero_of_eq h (subset.refl _) hi,
  push_neg at hne,
  have dxy_pos : 0 < dist x y, from dist_pos.2 (mt (Î» h, congr_fun h 0) (hne 0)),
  have hlt : âˆ€ i, x i < y i := Î» i, (h i).lt_of_ne (hne i), clear hne,
  set V := âˆ i, (y i - x i),
  have Vpos : 0 < V, from finset.prod_pos (Î» i hi, sub_pos.2 (hlt _)),
  refine this.norm_subadditive_on.eq_zero_of_forall_is_o_prod h (Î» b hb, _), clear this,
  set T := ((fin (n + 1) â†’ â„) Ã— (fin (n + 1) â†’ â„)) Ã— â„,
  set L : filter T := (ğ“[Icc x b] b Ã—á¶  ğ“[Icc b y] b Ã—á¶  ğ“[Ioi (0 : â„)] 0) âŠ“
    ğ“Ÿ {p | p.fst.snd - p.fst.fst = p.snd â€¢ (y - x)},
  simp only [uncurry],
  have H1'' : âˆ€á¶  p : T in L, p.1.1 âˆˆ Icc x b âˆ§ p.1.2 âˆˆ Icc b y âˆ§ 0 < p.2,
  { have A : âˆ€á¶  z in ğ“[Icc x b] b, z âˆˆ Icc x b := self_mem_nhds_within,
    have B : âˆ€á¶  z in ğ“[Icc b y] b, z âˆˆ Icc b y := self_mem_nhds_within,
    have C : âˆ€á¶  Îµ : â„ in ğ“[Ioi 0] 0, 0 < Îµ := self_mem_nhds_within,
    simpa only [and.assoc] using (inf_le_left : L â‰¤ _) ((A.prod_mk B).prod_mk C) },
  have H1 : âˆ€á¶  p : T in L, p.1.1 âˆˆ Icc x b âˆ§ p.1.2 âˆˆ Icc b y,
    from H1''.mono (Î» p hp, âŸ¨hp.1, hp.2.1âŸ©),
  have H1' : âˆ€á¶  p : T in L, p.1.1 â‰¤ p.1.2 := H1.mono (Î» p hp, hp.1.2.trans hp.2.1),
  have H2 : tendsto (Î» p : T, p.1.1) L (ğ“[Icc x b] b),
    from (tendsto_fst.comp tendsto_fst).mono_left inf_le_left,
  have H2' : tendsto (Î» p : T, p.1.1) L (ğ“[Icc x y] b),
    from H2.mono_right (nhds_within_mono _ $ Icc_subset_Icc_right hb.2),
  have H3 : tendsto (Î» p : T, p.1.2) L (ğ“[Icc b y] b),
    from (tendsto_snd.comp tendsto_fst).mono_left inf_le_left,
  have H3' : tendsto (Î» p : T, p.1.2) L (ğ“[Icc x y] b),
    from H3.mono_right (nhds_within_mono _ $ Icc_subset_Icc_left hb.1),
  have H4 : tendsto (Î» p : T, Icc p.1.1 p.1.2) L ((ğ“[Icc x y] b).lift' powerset), from H2'.Icc H3',
  have H5 : (Î» p : T, (volume (Icc p.1.1 p.1.2)).to_real) =á¶ [L] (Î» p, âˆ i, (p.1.2 i - p.1.1 i)),
    from H1'.mono (Î» p, real.volume_Icc_pi_to_real),
  have H6 : âˆ€á¶  p : T in L, âˆ€ i, p.1.2 i - p.1.1 i = p.2 * (y i - x i),
    from eventually_inf_principal.2 (eventually_of_forall (Î» p hp, congr_fun hp)),
  have H7 : (Î» p : T, âˆ i, (p.1.2 i - p.1.1 i)) =á¶ [L] (Î» p, V â€¢ p.2 ^ (n + 1)),
  { refine H6.mono (Î» p hp, (finset.prod_congr rfl (Î» i _, hp i)).trans _),
    simp [finset.prod_mul_distrib, mul_comm, V] },
  refine (hf'c.integral_sub_linear_is_o_ae hb compact_pi_Icc.is_measurable H4 _ H5).triangle _,
  suffices : is_o _ _ L, from this.congr' (eventually_eq.refl _ _) H7.symm,
  set df : (fin (n + 1) â†’ â„) â†’áµƒ[â„] (fin (n + 1) â†’ E) :=
    (affine_equiv.const_vadd â„ (fin (n + 1) â†’ E) (f b)).to_affine_map.comp
      ((f' b).to_linear_map.to_affine_map.comp (affine_equiv.vadd_const â„ b).symm.to_affine_map),
  have hdf : âˆ€ z, df z = f b + f' b (z - b), from Î» z, rfl,
  have hdfc : continuous df,
    from continuous_const.add ((f' b).continuous.comp (continuous_id.sub continuous_const)),
  have H8 :
    is_o (Î» p : T, (âˆ i, (p.1.2 i - p.1.1 i)) â€¢ divf b - box_boundary_integral df p.1.1 p.1.2)
      (Î» p, V â€¢ p.2 ^ (n + 1)) L,
  { refine (is_o_zero _ _).congr' (H1'.mono $ Î» p hp, _) (eventually_eq.refl _ _),
    refine (sub_eq_zero.2 _).symm,
    simpa using (df.box_boundary_integral hp hdfc).symm },
  refine H8.triangle (is_o.symm _), clear H8,
  have H9 : (Î» p : T, box_boundary_integral (Î» z, f z - f b - f' b (z - b)) p.1.1 p.1.2) =á¶ [L]
    (Î» p : T, box_boundary_integral f p.1.1 p.1.2 - box_boundary_integral df p.1.1 p.1.2),
  { refine H1.mono (Î» p hp, _),
    simp only [â† box_boundary_integral_sub (hp.1.2.trans hp.2.1)
      (hfc.mono $ Icc_subset_Icc hp.1.1 hp.2.2) hdfc.continuous_on],
    simp only [hdf, sub_sub] },
  refine is_o.congr' H9 (eventually_eq.refl _ _) (is_o.sum $ Î» i hi, _), clear hi,
  set g := Î» z, f z - f b - (f' b) (z - b),
  have hg : is_o g (Î» z, z - b) (ğ“[Icc x y] b) := hd b hb,
  suffices : âˆ€ m : T â†’ â„, (âˆ€á¶  p in L, m p âˆˆ Icc (p.1.1 i) (p.1.2 i)) â†’
    is_o (Î» p : T,
      âˆ« z in Icc (p.1.1 âˆ˜ i.succ_above) (p.1.2 âˆ˜ i.succ_above), g (i.insert_nth (m p) z) i)
      (Î» p, V â€¢ p.2 ^ (n + 1)) L,
  { refine (this (Î» p, p.1.2 i) _).sub (this (Î» p, p.1.1 i) _),
    exacts [H1'.mono (Î» p hp, right_mem_Icc.2 (hp i)), H1'.mono (Î» p hp, left_mem_Icc.2 (hp i))] },
  refine Î» m hm, is_o_iff.2 (Î» C Cpos, _),
  set Ci := C * (y i - x i),
  have Cipos : 0 < Ci, from mul_pos Cpos (sub_pos.2 $ hlt i),
  filter_upwards [H1'', H6, hm,
    tendsto_lift'.1 H4 _ (hg.def (div_pos Cipos dxy_pos))],
  rintro âŸ¨âŸ¨l, uâŸ©, ÎµâŸ© âŸ¨âŸ¨hxl, hlbâŸ©, âŸ¨hbu, huyâŸ©, Îµ0âŸ© hsub_eq hlmu hC,
  dsimp only at *,
  have : âˆ€ z âˆˆ Icc (l âˆ˜ i.succ_above) (u âˆ˜ i.succ_above),
    âˆ¥g (i.insert_nth (m ((l, u), Îµ)) z) iâˆ¥ â‰¤ Ci * Îµ,
  { rintros z hz,
    set m' := m ((l, u), Îµ),
    have : i.insert_nth m' z âˆˆ Icc l u, from fin.insert_nth_mem_Icc.2 âŸ¨hlmu, hzâŸ©,
    calc âˆ¥g (i.insert_nth m' z) iâˆ¥ â‰¤ âˆ¥g (i.insert_nth m' z)âˆ¥ : norm_le_pi_norm _ _
    ... â‰¤ Ci / dist x y * âˆ¥(i.insert_nth m' z) - bâˆ¥ : hC this
    ... â‰¤ Ci * Îµ : _,
    rw [div_eq_mul_inv, mul_assoc, â† div_eq_inv_mul, mul_le_mul_left Cipos, div_le_iff dxy_pos,
      â† dist_eq_norm],
    calc dist (i.insert_nth m' z) b â‰¤ dist l u : real.dist_pi_le_of_mem_Icc this âŸ¨hlb, hbuâŸ©
    ... â‰¤ Îµ * dist x y : (dist_pi_le_iff (mul_nonneg Îµ0.le dist_nonneg)).2 (Î» j, _),
    simp only [dist_eq_norm', real.norm_eq_abs, hsub_eq, abs_mul, abs_of_pos Îµ0],
    exact mul_le_mul_of_nonneg_left (norm_le_pi_norm (y - x) j) Îµ0.le },
  refine (norm_set_integral_le_of_norm_le_const' _ compact_pi_Icc.is_measurable this).trans _,
  { rw [real.volume_Icc_pi], exact ennreal.prod_lt_top (Î» i hi, ennreal.of_real_lt_top) },
  simp only [Ci, mul_assoc],
  rw [real.volume_Icc_pi_to_real, mul_le_mul_left Cpos], swap, exact Î» j, (hlb.trans hbu) _,
  calc _ = _ : _
  ... â‰¤ _ : le_abs_self _,
  simp only [hsub_eq, finset.prod_mul_distrib, finset.prod_const, finset.card_fin,
    fin.prod_univ_succ_above _ i, V, pow_succ, smul_eq_mul],
  ac_refl
end

theorem box_boundary_integral_eq_fderiv_within (h : x â‰¤ y)
  (hd : differentiable_on â„ f (Icc x y))
  (hf'c : continuous_on (Î» z, âˆ‘ i, fderiv_within â„ f (Icc x y) z (update 0 i 1) i) (Icc x y)) :
  box_boundary_integral f x y =
    âˆ« z in Icc x y, âˆ‘ i, fderiv_within â„ f (Icc x y) z (update 0 i 1) i :=
box_boundary_integral_eq_of_has_fderiv_within_at h hf'c $ Î» z hz, (hd z hz).has_fderiv_within_at

theorem box_boundary_integral_eq_fderiv (h : x â‰¤ y)
  (hd : âˆ€ z âˆˆ Icc x y, differentiable_at â„ f z)
  (hf'c : continuous_on (Î» z, âˆ‘ i, fderiv â„ f z (update 0 i 1) i) (Icc x y)) :
  box_boundary_integral f x y = âˆ« z in Icc x y, âˆ‘ i, fderiv â„ f z (update 0 i 1) i :=
box_boundary_integral_eq_of_has_fderiv_within_at h hf'c $
  Î» z hz, (hd z hz).has_fderiv_at.has_fderiv_within_at
